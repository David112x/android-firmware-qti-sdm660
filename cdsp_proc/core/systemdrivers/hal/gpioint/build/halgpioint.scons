#===============================================================================
#           Copyright 2009-2015 QUALCOMM Technologies Incorporated.
#                           All Rights Reserved.
#                         QUALCOMM Proprietary/GTDR
#=============================================================================
# $Header: //components/rel/core.qdsp6/1.0.c2/systemdrivers/hal/gpioint/build/halgpioint.scons#1 $
#===============================================================================
# HAL GPIOINT Libs
#-------------------------------------------------------------------------------
Import('env')
env = env.Clone()
  
env.PublishPrivateApi("SYSTEMDRIVERS_HAL_GPIOINT_INC", [
   "${INC_ROOT}/core/systemdrivers/hal/gpioint/inc",
   "${INC_ROOT}/core/systemdrivers/hal/gpioint/src",
   "${INC_ROOT}/core/systemdrivers/hal/tlmmtile/inc"
  ])

#-------------------------------------------------------------------------------
# Source PATH
#-------------------------------------------------------------------------------

SRCPATH = "${BUILD_ROOT}/core/systemdrivers/hal/gpioint"

env.VariantDir('${BUILDPATH}', SRCPATH, duplicate=0) 

CBSP_API = [
   'DAL',
   'SERVICES',
   'SYSTEMDRIVERS',

   # needs to be last also contains wrong comdef.h
   'KERNEL',
]

env.RequirePublicApi(CBSP_API)
env.RequireRestrictedApi(CBSP_API)

#-------------------------------------------------------------------------------
# Image Definitions
#-------------------------------------------------------------------------------
HALGPIOINT_IMAGES = ['CORE_ADSP_ROOT', 'CORE_ADSP_USER','CORE_SLPI_ROOT', 'CORE_SLPI_USER']

#-------------------------------------------------------------------------------
# Internal depends within CoreBSP
#-------------------------------------------------------------------------------
env.UsesInc([
   "${HAL_API}",
   "${SERVICES_API}",
   "${SYSTEMDRIVERS}",
   "${SYSTEMDRIVERS_API}",
   "${KERNEL_API}",
])

env.UsesInc([
   "${HAL_LEGACY}",
   # needs to be last also contains wrong comdef.h
   "${KERNEL_LEGACY}",
])

#-------------------------------------------------------------------------------
# Sources, libraries
#-------------------------------------------------------------------------------

GPIO_SOURCES = [
   '${BUILDPATH}/hw/${CHIPSET}/${IMAGE}/HALgpiointPlatform.c',
   '${BUILDPATH}/src/HALgpioint.c'  
   ]

#-------------------------------------------------------------------------------
# Add Libraries to image
#-------------------------------------------------------------------------------

env.AddLibrary(HALGPIOINT_IMAGES, '${BUILDPATH}/HALgpioint', GPIO_SOURCES)

# ============================================================================
# HWIO
# ============================================================================

if env.has_key('HWIO_IMAGE'):

  env.AddHWIOFile('HWIO', [
    {
      'filename': '${INC_ROOT}/core/systemdrivers/hal/gpioint/hw/${CHIPSET}/${IMAGE}/HALgpiointHWIO.h',
      'bases': ['TLMM_TLMM'],
      'filter-include': ['GPIO_INTR', 'DIR_CONN'],
      'filter-exclude': ['ACR'],
      'header': 'extern uint32 nGPIOIntBaseAddress;\n' +
                '#define TLMM_TLMM_BASE nGPIOIntBaseAddress\n'
    }
  ])

#---------------------------------------------------------------------------
# Pack out target specific files not needed
#---------------------------------------------------------------------------

# Remove target specific hardware files
ALL_HW_FILES = env.FindFiles(
  ['*'],
  '../hw/')
MY_HW_FILES = env.FindFiles(
  ['*'],
  '../hw/${CHIPSET}/${IMAGE}')
PACK_HW_FILES = list(set(ALL_HW_FILES) - set(MY_HW_FILES))
env.CleanPack(HALGPIOINT_IMAGES, PACK_HW_FILES)



