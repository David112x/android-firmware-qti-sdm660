#===============================================================================
#
# POWER Libs
#
# GENERAL DESCRIPTION
#    build script
#
# Copyright (c) 2009-2013,2015-2017 by QUALCOMM Technologies, Incorporated.
# All Rights Reserved.
# QUALCOMM Proprietary/GTDR
#
#===============================================================================
Import('env')
import os
env = env.Clone()

if env['CHIPSET'] in 'msm8998':
   env.LoadSoftwareUnits(0,"../nazgul/${IMAGE}/build") 
   Return()

#-------------------------------------------------------------------------------
# Convert warnings to errors 
#-------------------------------------------------------------------------------
env.Append(CFLAGS = "-Werror ")

# The LLVM compiler added in several dozen -Wunused-function warnings with entries
# in device configuration.  We will suppress these warnings for the devcfg_img
# SCons image only.
if env.CheckAlias('devcfg_img'):
   env.Append(CFLAGS = ' -Wno-unused-function')


"""
  Determine which processor the currently build is for using the
  actively building image. There cannot be more than one of the
  same name within a target.  Meaning, 7x30 cannot have two 
  processors named apps.

  Possible processors are:

  apps - Applications
  modem - Modem Functionality 
  modemCtrl - Modem Controller
  rpm - Resource Power Manager
  lpa - Low Power Audio
  riva - wireless connectivity
"""

target_images = [
    (['default'],
        {
            'modem'   : ['SINGLE_IMAGE', 'CBSP_SINGLE_IMAGE',
                         'MODEM_IMAGE', 'CBSP_MODEM_IMAGE',
                         'QDSP6_SW_IMAGE', 'CBSP_QDSP6_SW_IMAGE'],
            'adsp'    : ['SINGLE_IMAGE', 'CBSP_SINGLE_IMAGE',
                         'MODEM_IMAGE', 'CBSP_MODEM_IMAGE',
                         'QDSP6_SW_IMAGE', 'CBSP_QDSP6_SW_IMAGE',
                         'CORE_QDSP6_SENSOR_SW', 'CORE_QDSP6_AUDIO_SW'],
            'apps'    : ['APPS_IMAGE', 'CBSP_APPS_IMAGE'],
            'rpm'     : ['RPM_IMAGE'],
            'riva'    : ['WCN_IMAGE', 'CBSP_WCN_IMAGE'],
            'sensors' : ['CORE_SPS'],
            'sbl3'    : ['SBL3_BOOT_IMAGE'],
        },
    ),
    (['8200', '8900', '9x00', '8220'],
        {
            'modem'     : ['QDSP6_SW_IMAGE', 'CBSP_QDSP6_SW_IMAGE'],
            'modemCtrl' : ['MODEM_IMAGE', 'CBSP_MODEM_IMAGE'],
        },
    ),
]

def find_image_list(hal_platform):
    target_specific_images = [x[1] for x in target_images if hal_platform in x[0]]
    default_images = [x[1] for x in target_images if 'default' in x[0]]
    return (target_specific_images + default_images)[0]

def determine_processor_name(env):
  target_images = find_image_list(env['HAL_PLATFORM'])

  env_var_names = set(env.gvars().keys())

  for image in target_images:
    if (env_var_names.intersection(set(target_images[image]))):
      return image

  return 'unknown'

env.AddMethod(determine_processor_name, 'DetermineProcessorName')

#Protected APIs are only accessible to units inside power
power_utils_protected_apis = [
  "${INC_ROOT}/core/power/utils/inc",
]

env.PublishProtectedApi('POWER_UTILS', power_utils_protected_apis)

#Sleep protected APIs
power_sleep_protected_apis = [
  "${INC_ROOT}/core/power/sleep2.0/inc",
]

if 'USES_ISLAND' in env:
  power_sleep_protected_apis.append("${INC_ROOT}/core/power/uSleep/inc")

env.PublishProtectedApi('POWER_SLEEP', power_sleep_protected_apis)

#RPM protected APIs
power_rpm_protected_apis = [
  "${INC_ROOT}/core/power/rpm/inc",
  "${INC_ROOT}/core/power/rpm/inc/${MSM_ID}",
]

env.PublishProtectedApi('POWER_RPM', power_rpm_protected_apis)
if env['IMAGE'] == "cdsp":
  env.Append(CPPDEFINES=['VMPM_MASTER=VMPM_SSC'])
else:
  env.Append(CPPDEFINES=['VMPM_MASTER=VMPM_ADSP'])

#MPM protected API's
env.PublishProtectedApi('POWER_MPM', [
   "${INC_ROOT}/core/power/mpm/inc",
   "${INC_ROOT}/core/power/mpm/inc/asic/${MSM_ID}",
])


#Flag for determining the presence of Island Mode
if 'USES_ISLAND' in env:
   env.Append(CPPDEFINES=['USE_ISLAND'])

#QuRT user PD Flags
if 'USES_MULTI_PD' in env and 'USES_QURTOS_IMG' not in env:
  env.Append(CPPDEFINES=['-DUTILS_USE_MPD_APP'])
  env.Append(CPPDEFINES=['-DUSES_QURT'])

# Cleaning other target folders
CLEANSRCFILES = env.FindFiles('*', "${INC_ROOT}/core/power/nazgul")
CLEANAPIFILES = env.FindFiles('*', "${INC_ROOT}/core/api/power/nazgul")
env.CleanPack('CORE_Q6_ROOT', CLEANSRCFILES)
env.CleanPack('CORE_Q6_ROOT', CLEANAPIFILES)
  
#-------------------------------------------------------------------------------
# Load sub scripts
#-------------------------------------------------------------------------------
env.LoadSoftwareUnits()
