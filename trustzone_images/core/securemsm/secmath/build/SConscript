#===============================================================================
#
# secmath
#
# GENERAL DESCRIPTION
#    build script
#
# Copyright (c) 20011 by QUALCOMM, Incorporated.
# All Rights Reserved.
# QUALCOMM Proprietary/GTDR
#
#-------------------------------------------------------------------------------
#
#  $Header:  $
#  $DateTime: $
#  $Author:  $
#  $Change:  $
#                      EDIT HISTORY FOR FILE
#
#  This section contains comments describing changes made to the module.
#  Notice that changes are listed in reverse chronological order.
#
# when       who     what, where, why
# --------   ---     ---------------------------------------------------------
# 11/25/15   rp     Linked to winsecapp
# 07/15/14   ck     Added --restrict flagh for hdcpsrm app compile.
# 12/22/11   vg     Removed publish restricted API as this is getting done in
#                   paths under core/securemsm.
# 06/16/11   vg     Added AUTH_BOOT_DRIVER to link to boot loaders for future
#                   targets
# 06/16/11   vg     Fixed for pack and strip build.
# 05/12/11   vg     initial version
#===============================================================================
Import('env')
env = env.Clone()


#-------------------------------------------------------------------------------
# Set search path for securemsm subsystem files
#-------------------------------------------------------------------------------
env.Append(CPPPATH = "${COREBSP_ROOT}/securemsm/secmath/shared/inc")
env.Append(CCFLAGS = " -O3  ")

#------------------------------------------------------------------------------
# We need to specify "neon" to generate SIMD instructions in 32-bit mode
#------------------------------------------------------------------------------
if env['PROC'] == 'scorpion':
  env.Append(CCFLAGS = " -mfpu=neon ")

#-------------------------------------------------------------------------------
# External depends within CoreBSP
#-------------------------------------------------------------------------------
CBSP_API = [
   'SECUREMSM',
   'SERVICES',

   # needs to be last also contains wrong comdef.h
   'KERNEL',
]

env.RequirePublicApi(CBSP_API)
env.RequireRestrictedApi(CBSP_API)

#-------------------------------------------------------------------------------
# Source PATH
#-------------------------------------------------------------------------------
SRCPATH = "${COREBSP_ROOT}/securemsm/secmath"

#-------------------------------------------------------------------------------
# Let Scons build the object files in $(BUILDPATH} and don't copy over files
#-------------------------------------------------------------------------------
env.VariantDir('${BUILDPATH}', SRCPATH, duplicate=0)

#-------------------------------------------------------------------------------
# Compiler, object, and linker definitions
#-------------------------------------------------------------------------------



#-------------------------------------------------------------------------------
# Sources, libraries
#-------------------------------------------------------------------------------
SECMATH_SRC_SHARED = [
  '${BUILDPATH}/shared/src/secmath_mod_exp.c',
  '${BUILDPATH}/shared/src/secmath_montmul_ref.c',
  '${BUILDPATH}/shared/src/secmath_montmul_utils.c',
  '${BUILDPATH}/shared/src/secmath_barrett_reduce.c',
  '${BUILDPATH}/shared/src/secmath_BIGINT_read_radix.c',
  '${BUILDPATH}/shared/src/secmath_BIGINT_read_unsigned_bin.c',
  '${BUILDPATH}/shared/src/secmath_BIGINT_to_radix.c',
  '${BUILDPATH}/shared/src/secmath_BIGINT_to_unsigned_bin.c',
  '${BUILDPATH}/shared/src/secmath_BIGINT_is_prime.c',
  '${BUILDPATH}/shared/src/secmath_BIGINT_prime_test.c',
  '${BUILDPATH}/shared/src/secmath_is_prime_div_chk.c',
  '${BUILDPATH}/shared/src/secmath_bin_to_hex_str.c',
  '${BUILDPATH}/shared/src/secmath_hex_str_to_bin.c',
]

# This depends on the random number generator provided by TZ
if env.has_key('TZOS_IMAGE') or env.has_key('TZBSPTEST_IMAGE'):
   env.Append(CPPPATH = "${COREBSP_ROOT}/securemsm/secmath/env/tz/inc")
if env.has_key('CTZL_IMAGE') or env.has_key('CTZL64_IMAGE'):
   env.Append(CCFLAGS = " -DTZ_APP_LEGACY")
   env.Append(CPPPATH = "${COREBSP_ROOT}/api/securemsm/trustzone/qsee")
   env.Append(CPPPATH = "${COREBSP_ROOT}/securemsm/secmath/env/sampleapp/inc")

SECMATH_PRIV_HEADERS = env.GlobFiles('${BUILDPATH}/shared/src/*.h')

#-------------------------------------------------------------------------------
# Add Libraries to images in the secboot3 boot architecture. Note we don't
# add the library into SBL1 image as SBL1 uses PBL secboot library instead
#-------------------------------------------------------------------------------
env.LoadSoftwareUnits()

env.AddBinaryLibrary(['TZOS_IMAGE', 'CTZL_IMAGE', 'CTZL64_IMAGE', 'TZBSPTEST_IMAGE'],
	 '${BUILDPATH}/secmath',
	 SECMATH_SRC_SHARED)
env.CleanPack(['TZOS_IMAGE', 'CTZL_IMAGE', 'CTZL64_IMAGE'], SECMATH_PRIV_HEADERS)
ALL_SRC = env.FindFiles(['*.c'], "${BUILDPATH}/")
CLEAN_SOURCES = list(set(ALL_SRC) - set(SECMATH_SRC_SHARED))
env.CleanPack(['TZOS_IMAGE', 'CTZL_IMAGE', 'CTZL64_IMAGE'], CLEAN_SOURCES)

env.Deploy('../shared/inc/secmath.h')